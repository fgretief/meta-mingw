FILESEXTRAPATHS_prepend := "${THISDIR}/${PN}:"

SRC_URI_append_mingw32 = " \
    file://ncurses-5.9-branch_update-4.patch \
    "

EXCONFIG_ARGS_append_mingw32 = " \
	--enable-warnings \
	--enable-assertions \
	--without-cxx-binding \
	--disable-home-terminfo \
	--enable-reentrant \
	--enable-sp-funcs \
	--enable-term-driver \
	--enable-interop \
	"

do_configure_append_mingw32() {
    # fixup SHLIB_LIST variables
    sed -e "s|^SHLIB_LIST\t=.*$|SHLIB_LIST\t= \$\(SHLIB_DIRS\) -ltict -ltinfo|" \
        -e "s|^TINFO_LIST\t=.*$|TINFO_LIST\t= \$\(SHLIB_DIRS\) -lncursest -ltict|" \
        -e "s|^TICS_LIST\t=.*$|TICS_LIST\t= \$\(SHLIB_DIRS\) -lncursest -ltinfo|" \
        -i ${B}/narrowc/ncurses/Makefile
    sed -e "s|^SHLIB_LIST\t=.*$|SHLIB_LIST\t= \$\(SHLIB_DIRS\) -ltictw -ltinfo|" \
        -e "s|^TINFO_LIST\t=.*$|TINFO_LIST\t= \$\(SHLIB_DIRS\) -lncursestw -ltictw|" \
        -e "s|^TICS_LIST\t=.*$|TICS_LIST\t= \$\(SHLIB_DIRS\) -lncursestw -ltinfo|" \
        -i ${B}/widec/ncurses/Makefile
}

shell_do_install_mingw32() {
	# Order of installation is important; widec installs a 'curses.h'
	# header with more definitions and must be installed last hence.
	# Compatibility of these headers will be checked in 'do_test()'.
	oe_runmake -C narrowc ${_install_cfgs} ${_install_opts} \
		install.progs

	# The install.data should run after install.libs, otherwise
	# there would be a race issue in a very critical conditon, since
	# tic will be run by install.data, and tic needs libtinfo.so
	# which would be regenerated by install.libs.
	oe_runmake -C narrowc ${_install_cfgs} \
		install.data

	! ${ENABLE_WIDEC} || \
		oe_runmake -C widec ${_install_cfgs} ${_install_opts}

	cd narrowc
	
	# include some basic terminfo files
	# stolen ;) from gentoo and modified a bit
	for x in ansi console dumb linux rxvt screen sun vt{52,100,102,200,220} xterm-color xterm-xfree86 xterm-256color
	do
		local termfile="$(find "${D}${datadir}/terminfo/" -name "${x}" 2>/dev/null)"
		local basedir="$(basename $(dirname "${termfile}"))"

		if [ -n "${termfile}" ]
		then
			install -d ${D}${sysconfdir}/terminfo/${basedir}
			mv ${termfile} ${D}${sysconfdir}/terminfo/${basedir}/
			ln -s /etc/terminfo/${basedir}/${x} \
				${D}${datadir}/terminfo/${basedir}/${x}
		fi
	done
	# i think we can use xterm-color as default xterm
	if [ -e ${D}${sysconfdir}/terminfo/x/xterm-color ]
	then
		ln -sf xterm-color ${D}${sysconfdir}/terminfo/x/xterm
	fi

	rm -f ${D}${libdir}/terminfo
	
	## removed a section here. must become DLL spesific

	oe_multilib_header curses.h
}

python populate_packages_prepend_mingw32 () {
    bindir = d.expand("${bindir}")
    base_bindir = d.expand("${base_bindir}")
    pnbase = d.expand("${PN}-lib%s")
    do_split_packages(d, bindir, '^lib(.*)\.dll', pnbase, 'ncurses %s library', prepend=True, extra_depends = '', allow_links=True)
    if bindir is not base_bindir:
        do_split_packages(d, base_bindir, '^lib(.*)\.dll', pnbase, 'ncurses %s library', prepend=True, extra_depends = '', allow_links=True)
}

FILES_${PN}_mingw32 = "\
    ${bindir}/tput.exe \
    ${bindir}/tset.exe \
    ${bindir}/ncurses5-config \
    ${bindir}/ncursesw5-config \
    ${datadir}/tabset \
    "

# This keeps only tput/tset in ncurses
# clear/reset are in already busybox
FILES_${PN}-tools_mingw32 = "\
    ${bindir}/tic.exe \
    ${bindir}/toe.exe \
    ${bindir}/infotocap.exe \
    ${bindir}/captoinfo.exe \
    ${bindir}/infocmp.exe \
    ${bindir}/clear.exe${@['', '.${BPN\x7d']['${CLASSOVERRIDE}' == 'class-target']} \
    ${bindir}/reset.exe${@['', '.${BPN\x7d']['${CLASSOVERRIDE}' == 'class-target']} \
    ${bindir}/tack.exe \
    ${bindir}/tabs.exe \
    "
